// Begin script om te zien hoe makkelijk versie beheer werkt en daarnaast een aantal voorbeelden te geven van datamodelleren binnen Qlik
//  <----------------------------------------------------------------->    ||     <----------------------------------------------------------------->
SET ThousandSep='.';
SET DecimalSep=',';
SET MoneyThousandSep='.';
SET MoneyDecimalSep=',';
SET MoneyFormat='€ #.##0,00;€ -#.##0,00';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD-MM-YYYY';
SET TimestampFormat='DD-MM-YYYY hh:mm:ss[.fff]';
SET FirstWeekDay=0;
SET BrokenWeeks=0;
SET ReferenceDay=4;
SET FirstMonthOfYear=1;
//  <----------------------------------------------------------------->    ||     <----------------------------------------------------------------->

Sub Variabelen
    // <----------------------------------------------------------------->  
    // Variabele voor het connectiepad:
    Set vPath =                         'lib://Datamodelleren';
    set vHuidigeJaar =                  Num(Today());
    Set vBeginJaar =                    Num(MakeDate(Year(Today())-2));

    // <-----------------------------------------------------------------> 
End Sub

//  <----------------------------------------------------------------->    ||     <----------------------------------------------------------------->

Sub Opdracht_1
    // <-----------------------------------------------------------------> 
    key_table:
    LOAD
        AutoNumberHash128(MedewerkerID & AfdelingID)                        as  _Key_1,
        AutoNumberHash128(MedewerkerID & '#' & AfdelingID)                  as  _Key_2,
        RowNo()                                                             as  _Key_3,
        RecNo()                                                             as  _Key_4,
        MedewerkerID,
        AfdelingID
    FROM 
        [$(vPath)/1. Sleutelvelden/Practice_Sleutelvelden.csv](txt, utf8, embedded labels, delimiter is ',', msq)
    ;

    // <-----------------------------------------------------------------> 
End Sub

//  <----------------------------------------------------------------->    ||     <----------------------------------------------------------------->

Sub Opdracht_2
    // <-----------------------------------------------------------------> 
    Kalender_TMP:
    LOAD
        DATE($(vBeginJaar) + ROWNO() - 1)                                   as TempDate
    AUTOGENERATE
        $(vHuidigeJaar) - $(vBeginJaar) + 1
    ;	
    
    Kalender:
    LOAD
        TempDate 								                            as Date,
        WEEK(TempDate) 							                            as Week,
        YEAR(TempDate) 							                            as Year,
        MONTH(TempDate) 						                            as Month,
        DAY(TempDate) 							                            as Day,
        WEEKDAY (TempDate) 						                            as Weekday,
        'Q' & CEIL(MONTH(TempDate) / 3) 		                            as Quarter,
        DATE(MONTHSTART(TempDate), 'MMM-YYYY') 	                            as MonthYear,
        WEEK(TempDate) & '-' & YEAR(TempDate) 	                            as WeekYear
    RESIDENT 
        Kalender_TMP;
    
    DROP TABLE Kalender_TMP;

    Dienstverband_TMP:
    LOAD
        *,
        If(Start_datum + IterNo() -1 = Start_datum,
            1,
                0)								                            as #InDienst,
        Start_datum + IterNo() -1				                            as Date
    While 
        Start_datum + IterNo() -1 <= Eind_datum
    ;      
    
    LOAD
        MedewerkerID,
        ContractID,
    //       Date("Startdatum Contract")			as	Date,
        Date("Startdatum Contract")			                                as	Start_datum,
        If(Len("Einddatum Contract") < 1,
            Date(Today()),
                "Einddatum Contract")			                            as	Eind_datum
    FROM
        [$(vPath)/2. Van Dimensie naar Feit/Van_Dimensie_Naar_Feit.xlsx](ooxml, embedded labels, table is Blad1)
    ;

    // <-----------------------------------------------------------------> 
End Sub

//  <----------------------------------------------------------------->    ||     <----------------------------------------------------------------->
// Aanroepen subroutines
    CALL Variabelen
    //CALL Opdracht_1
    CALL Opdracht_2

//  <----------------------------------------------------------------->    ||     <----------------------------------------------------------------->